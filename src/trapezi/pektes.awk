#!/usr/bin/env awk

# Το πρόγραμμα διαβάζει γραμμές της μορφής:
#
#	Τραπέζι (κωδικός παρτίδας)
#	Παίκτης (1)
#	Παίκτης (2)
#	Παίκτης (3)
#
# και εκτυπώνει στο output κωδικούς παρτίδας χωρίς τα ονόματα των παικτών αφού
# πρώτα «φιλτράρει» τις παρτίδες στις οποίες συμμετέχουν οι παίκτες που έχουν
# καθοριστεί στο command line.
#
# Το πρόγραμμα δέχεται ως command line arguments ονόματα παικτών και «φιλτράρει»
# τις παρτίδες στις οποίες συμμετέχουν οι συγκεκριμένοι παίκτες. Ο τρόπος με τον
# οποίο γίνεται το ξεδιάλεγμα είναι λίγο ιδιόμορφος: Αν οι παίκτες είναι λιγότεροι
# από τρεις, τότε το πρόγραμμα ξεδιαλέγει παρτίδες στις οποίες συμμετέχουν όλοι
# οι παίκτες που έχουν καθοριστεί. Αν οι παίκτες είναι από τρεις και πάνω, τότε
# το πρόγραμμα ξεδιαλέγει παρτίδες στις οποίες όλοι οι εμπλεκόμενοι παίκτες έχουν
# καθοριστεί.
#
# Παράδειγμα Α: Έχουμε καθορίσει στο command line τον παίκτη panos. Το πρόγραμμα
# θα ξεδιαλέξει παρτίδες στις οποίες συμμετέχει ο παίκτης panos.
#
# Παράδειγμα Β: Έχουμε καθορίσει στο command line τους παίκτες panos και maria.
# Το πρόγραμμα θα ξεδιαλέξει παρτίδες στις οποίες συμμετέχουν και ο panos και η
# maria.
#
# Παράδειγμα Γ: Έχουμε καθορίσει στο command line τους παίκτες panos, maria, akis,
# nikos, takis και dora. Το πρόγραμμα θα ξεδιαλέξει παρτίδες στις οποίες όλοι οι
# παίκτες ανήκουν στην παραπάνω ομάδα.

BEGIN {
	FS = "\t"

	target_setup()
}

NF >= 4 {
	trapezi = $1 + 0

	pektis[1] = $2
	pektis[2] = $3
	pektis[3] = $4

	if (target_check())
	print trapezi
}

# Η function "target_check" ελέγχει αν το τραπέζι πληροί τις προϋποθέσεις που έχουν
# τεθεί όσον αφορά στους συμμετέχοντες παίκτες.

function target_check(		target, thesi, ok) {
	# Αν δεν έχουν καθοριστεί παίκτες στο command line, τότε εκτυπώνονται όλες
	# οι παρτίδες χωρίς να γίνεται κάποιο «φιλτράρισμα» που να αφορά στους
	# συμμετέχοντες παίκτες.

	if (target_count <= 0)
	return 1

	# Έχουν καθοριστεί παίκτες στο command line, επομένως ελέγχουμε τους παίκτες
	# που συμμετέχουν στην κάθε παρτίδα.

	for (thesi = 1; thesi <= 3; thesi++) {
		if (pektis[thesi] in target_list)
		ok++
	}

	if (ok >= target_count)
	return 1

	return (ok >= 3)
}

# Η function "target_setup" «στήνει» τη λίστα επιθυμητών παικτών θεωρώντας τα command line
# arguments ως ως login names. Αποτέλεσμα της συγκεκριμένης διαδικασίας είναι η λίστα των
# επιθυμητών παικτών "target_list" που είναι δεικτοδοτημένη με τα login names των εν λόγω
# παικτών, και το πλήθος των παικτών που έχουν καθοριστεί "target_count".

function target_setup(			target) {
	# Εισάγουμε αρχικά τον κενό παίκτη στη λίστα προκειμένου να αποφύγουμε συνεχείς
	# ελέγχους στο loop που ακολουθεί.

	target_list[""] = 0
	target_count = 0

	while (--ARGC > 0) {
		target = ARGV[ARGC]

		if (target in target_list)
		continue

		target_list[target] = target_count++
	}

	# Διαγράφουμε τον κενό παίκτη που είχαμε εισαγάγει στην αρχή της διαδικασίας.

	delete target_list[""]
}
